allprojects{
	group = 'org.requirementsascode'
	version = '2.0'
	
	apply plugin: 'java'

	compileJava {
	  sourceCompatibility = '8'
	  targetCompatibility = '8'
	}
}

subprojects{
	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'

	repositories {
		mavenCentral()
		maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
	}
	
	javadoc {
	    options.addBooleanOption("Xdoclint:-missing", true)
	}
	
	task javadocJar(type: Jar) {
	    classifier = 'javadoc'
	    from javadoc
	}
	
	task sourcesJar(type: Jar) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}
	
	artifacts {
	    archives javadocJar, sourcesJar
	}
	
	test {
		useJUnitPlatform()
	}
	
	dependencies {
		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
	}
	
	if (project.hasProperty("signing.keyId")) {
		apply plugin: 'signing'
	
		publishing {
			publications {
				mavenJava(MavenPublication) {
					pom{
						name = 'requirementsascodeextract'
						description = 'Enables you to define and run executable use case specifications, in your code.'
						url = 'http://requirementsascode.org/'
						packaging = 'jar'
						
						from components.java
						         				
	      				artifact javadocJar {
							classifier "javadoc"
	      				}
						
						artifact sourcesJar {
							classifier "sources"
	      				}
	
						licenses {
							license {
								name = 'The Apache Software License, Version 2.0'
								url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							}
						}
						
						developers {
							developer {
								id = 'bertilmuth'
								name = 'Bertil Muth'
							}
						}
						
						scm {
							url = 'scm:git@github.com:bertilmuth/requirementsascode.git'
							connection = 'scm:git@github.com:bertilmuth/requirementsascode.git'
							developerConnection = 'scm:git@github.com:bertilmuth/requirementsascode.git'
						}
					}
				}
			}
			
			repositories {
				maven {
					credentials {
						username sonatypeUsername
						password sonatypePassword
					}
					
					def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
					def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
					url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				}
			}
		}
		
		signing {
			sign publishing.publications.mavenJava
		}
	}
}
