apply plugin: 'maven'
apply plugin: 'signing'

jar {
    manifest {
        attributes 'Implementation-Title': 'requirements as code - extract',
                   'Implementation-Version': version
	}
}

dependencies {
    compile 'org.freemarker:freemarker:2.3.26-incubating'
    compile 'org.apache.commons:commons-lang3:3.5'
	compile 'org.requirementsascode:requirementsascodecore:' + version
  	testCompile 'junit:junit:4.12'
}

dependencies {
  	testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

import org.gradle.plugins.signing.Sign
gradle.taskGraph.whenReady { taskGraph ->
    
    // Do not sign archives by default (a local build without gpg keyring should succeed)
    if (taskGraph.allTasks.any { it.name == 'build' || it.name == 'assemble' }) {
        tasks.findAll { it.name == 'signArchives' || it.name == 'signDocsJar' || it.name == 'signTestJar' }.each { task ->
            task.enabled = false
        }
    }
}
signing {
    sign configurations.archives
}
build.dependsOn.remove(signArchives)

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			if (project.hasProperty('sonatypeUsername')){
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              		authentication(userName: sonatypeUsername, password: sonatypePassword)
            	}
            	snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
              		authentication(userName: sonatypeUsername, password: sonatypePassword)
            	}
            } else{
                 repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
             		authentication(userName: System.getenv('SONATYPE_USERNAME'), password: System.getenv('SONATYPE_PASSWORD'))
            	}
            	snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
              		authentication(userName: System.getenv('SONATYPE_USERNAME'), password: System.getenv('SONATYPE_PASSWORD'))
            	}
            }

            pom.project {
               name 'requirementsascodeextract'
               packaging 'jar'
               description 'Enables you to generate documentation from use case specifications in your code.'
               url 'http://requirementsascode.org/'

               scm {
                   url 'scm:git@github.com:bertilmuth/requirementsascode.git'
                   connection 'scm:git@github.com:bertilmuth/requirementsascode.git'
                   developerConnection 'scm:git@github.com:bertilmuth/requirementsascode.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'bertilmuth'
                       name 'Bertil Muth'
                   }
               }
           }
        }
    }
}